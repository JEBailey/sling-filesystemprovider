plugins {
    // apply plugin for OSGi R6/DS 1.3 support
    id "org.dm.bundle" version "0.8.4"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'groovy' // tests only
apply plugin: 'maven-publish'

group = 'net.kemuri9.sling'
version = '0.1-SNAPSHOT'

dependencies {
    compileOnly "org.osgi:org.osgi.service.component.annotations:${osgiDSVersion}"
    compileOnly "org.osgi:org.osgi.service.metatype.annotations:${osgiDSVersion}"
    compileOnly 'org.osgi:osgi.annotation:6.0.1'

    compile 'org.apache.sling:org.apache.sling.api:2.11.0'
    compile 'org.apache.sling:org.apache.sling.commons.classloader:1.3.2'
    compile 'org.apache.sling:org.apache.sling.commons.json:2.0.18'
    compile 'org.apache.sling:org.apache.sling.settings:1.3.8'
    compile "org.osgi:osgi.core:${osgiVersion}"
    compile "org.osgi:osgi.cmpn:${osgiVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"

    testCompile 'org.apache.sling:org.apache.sling.testing.osgi-mock:2.0.2'
    testCompile 'org.apache.sling:org.apache.sling.testing.sling-mock:1.6.2'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    testRuntime "org.slf4j:slf4j-simple:${slf4jVersion}"
    //testRuntime 'org.apache.sling:org.apache.sling.resourceaccesssecurity:1.0.0'
}

sourceSets.main.resources {
    srcDir file('.') include 'LICENSE'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bundle {
    instruction 'Export-Package', '!*.impl, net.kemuri9.*'
    instruction 'Bundle-Name', 'Filesystem Resource Provider'
}
